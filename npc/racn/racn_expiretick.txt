//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthenaCN Script =======================================
//= 获取装备租赁时间演示脚本
//===== By: ==================================================
//= Sola丶小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 开发团队
//===== Description: =========================================
//= 演示如何通过 getequipexpiretick 和 setrenttime 指令来获取、
//= 调整指定装备位置的剩余租赁时间
//============================================================
//= 此脚本演示的指令为:
//= [脚本] getequipexpiretick <装备位置编号>{,<角色编号>};
//=        为了兼容 SeAr 脚本 isrental 等效于 getequipexpiretick
//= [脚本] setrenttime <装备位置编号>,<要增加/减少的租赁时间秒数>{,<角色编号>};
//=        为了兼容 SeAr 脚本 resume 等效于 setrenttime
//============================================================

prontera,124,125,4	script	装备租赁时间	123,{
	mes "[演示人员]";
	mes "我负责演示获取装备剩余租赁时间.";
	mes "请选择您要的操作..";
	next;
	switch(select("租赁装备以便测试:获取装备剩余租赁时间:增加/减少装备的租赁时间")){
		case 1:
			mes "[演示人员]";
			mes "请填写要租赁的装备编号";
			next;
			input .@nameid;
			if (.@nameid <= 0) break;
			mes "[演示人员]";
			mes "请填写要租赁的时长(秒)";
			next;
			input .@renttime;
			if (.@renttime <= 0) break;
			mes "[演示人员]";
			mes "租赁完毕, 请穿戴装备, 随后再尝试获取装备的剩余租赁时间.";
			rentitem .@nameid, .@renttime;
			break;
		case 2:
			mes "[演示人员]";
			mes "请选择要获取剩余租赁时间的装备位置.";
			next;
			.@sel = select("头饰上:盔甲:角色左手(武器, 盾牌):角色右手(武器):披肩:鞋子:饰品(角色右侧):饰品(角色左侧):头饰中:头饰下:时装头饰下:时装头饰中:时装头饰上:时装披肩:箭矢/弹药:影子装备 - 盔甲:影子装备 - 武器:影子装备 - 盾牌:影子装备 - 鞋子:影子装备 - 饰品(角色左侧):影子装备 - 饰品(角色右侧)");
			
			.@ret = getequipexpiretick(.@sel);
			mes "[演示人员]";
			switch(.@ret){
				case -1:
					mes "这个位置没有穿戴任何装备";
					break;
				case 0:
					mes "这是普通装备, 不会过期";
					break;
				default:
					mes "剩余租赁时间为:" + isrental(.@sel) + " 秒";
					break;
			}
			break;
		case 3:
			mes "[演示人员]";
			mes "请选择要调整剩余租赁时间的装备位置.";
			next;
			.@sel = select("头饰上:盔甲:角色左手(武器, 盾牌):角色右手(武器):披肩:鞋子:饰品(角色右侧):饰品(角色左侧):头饰中:头饰下:时装头饰下:时装头饰中:时装头饰上:时装披肩:箭矢/弹药:影子装备 - 盔甲:影子装备 - 武器:影子装备 - 盾牌:影子装备 - 鞋子:影子装备 - 饰品(角色左侧):影子装备 - 饰品(角色右侧)");
			mes "[演示人员]";
			mes "- 要增加租赁时间, 请填写正数";
			mes "- 要减少租赁时间, 请填写负数";
			mes "- 租赁时间单位为: ^ff0000秒^000000";
			mes "- 填写 0 则取消操作";
			next;
			input .@renttime,-1000000,1000000;
			.@ret = setrenttime(.@sel, .@renttime);
			mes "[演示人员]";
			switch(.@ret){
				case -1:
					mes "这个位置没有穿戴任何装备";
					break;
				case 0:
					mes "这是普通装备, 无法调整租赁时间";
					break;
				default:
					mes "调整后的租赁时间为:" + .@ret + " 秒";
					break;
			}
			break;
	}
	close;
OnInit:
	waitingroom "装备租赁时间",1;
	end;
}